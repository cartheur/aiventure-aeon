<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01//EN" "http://www.w3.org/TR/html4/strict.dtd">
<html>
<head>
<meta http-equiv="Content-Type" content="text/html; charset=iso-8859-1">
<title>

  The Complete Syntax of Lua
</title>
<link href="doctorch.css" rel="stylesheet" type="text/css">
</head>

<body class="torchdoc">
<div id="container">

<div id="header">
<h3>
  The Complete Syntax of Lua</h3>
</div>

<div id="navline">
<a href="index.html">Torch Manual</a>&nbsp; > &nbsp; <a href="LuaManual.html">
Lua 5.1 Reference Manual</a>&nbsp; > &nbsp; <b>
  The Complete Syntax of Lua</b>
</div>

<div id="contents">
<a name="TheCompleteSyntaxofLua"></a>

<p>

<p>

<p>

 Here is the complete syntax of Lua in extended BNF. (It does not describe operator precedences.)
<pre>

	chunk ::= {stat [`*;*`]} [laststat [`*;*`]]

	block ::= chunk

	stat ::=  varlist1 `*=*` explist1 | 
		 functioncall | 
		 *do* block *end* | 
		 *while* exp *do* block *end* | 
		 *repeat* block *until* exp | 
		 *if* exp *then* block {*elseif* exp *then* block} [*else* block] *end* | 
		 *for* Name `*=*` exp `*,*` exp [`*,*` exp] *do* block *end* | 
		 *for* namelist *in* explist1 *do* block *end* | 
		 *function* funcname funcbody | 
		 *local* *function* Name funcbody | 
		 *local* namelist [`*=*` explist1] 

	laststat ::= *return* [explist1] | *break*

	funcname ::= Name {`*.*` Name} [`*:*` Name]

	varlist1 ::= var {`*,*` var}

	var ::=  Name | prefixexp `*[*` exp `*]*` | prefixexp `*.*` Name 

	namelist ::= Name {`*,*` Name}

	explist1 ::= {exp `*,*`} exp

	exp ::=  *nil* | *false* | *true* | Number | String | `*...*` | function | 
		 prefixexp | tableconstructor | exp binop exp | unop exp 

	prefixexp ::= var | functioncall | `*(*` exp `*)*`

	functioncall ::=  prefixexp args | prefixexp `*:*` Name args 

	args ::=  `*(*` [explist1] `*)*` | tableconstructor | String 

	function ::= *function* funcbody

	funcbody ::= `*(*` [parlist1] `*)*` block *end*

	parlist1 ::= namelist [`*,*` `*...*`] | `*...*`

	tableconstructor ::= `*{*` [fieldlist] `*}*`

	fieldlist ::= field {fieldsep field} [fieldsep]

	field ::= `*[*` exp `*]*` `*=*` exp | Name `*=*` exp | exp

	fieldsep ::= `*,*` | `*;*`

	binop ::= `*+*` | `*-*` | `***` | `*/*` | `*^*` | `*%*` | `*..*` | 
		 `*<*` | `*<=*` | `*>*` | `*>=*` | `*==*` | `*~=*` | 
		 *and* | *or*

	unop ::= `*-*` | *not* | `*#*`

</pre>
<p>

<p>

<p>

<hr>
   Last update: Thu Jan  4 08:59:33 BRST 2007
</div>



<div id="footer-left">&nbsp;<a href="LuaManual-7.html">
  Incompatibilities with the Previous Version</a></div>
<div id="footer-right"><a href=""></a>&nbsp;</div>

<div id="last-modified">

</div>

</div>
</body>
</html>
